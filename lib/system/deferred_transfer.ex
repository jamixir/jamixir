defmodule System.DeferredTransfer do
  # Formula (12.14) v0.6.4 - T

  @type t :: %__MODULE__{
          # s âˆˆ â„•_S
          sender: non_neg_integer(),
          # d âˆˆ â„•_S
          receiver: non_neg_integer(),
          # a âˆˆ â„•_B
          amount: non_neg_integer(),
          # m âˆˆ ğ•_W_T
          memo: binary(),
          # g âˆˆ â„•_G
          gas_limit: non_neg_integer()
        }

  defstruct sender: 0,
            receiver: 0,
            amount: 0,
            memo: <<0::size(Constants.memo_size() * 8)>>,
            gas_limit: 0

  # Formula (12.26) v0.6.4
  @spec select_transfers_for_destination(list(t()), non_neg_integer()) :: list(t())
  def select_transfers_for_destination(transfers, destination) do
    Enum.with_index(transfers)
    |> Enum.filter(fn {t, _} -> t.receiver == destination end)
    |> Enum.sort_by(fn {t, index} -> {t.sender, index} end)
    |> Enum.map(fn {t, _} -> t end)
  end

  defimpl Encodable do
    use Codec.Encoder
    alias System.DeferredTransfer
    # Formula (C.28) v0.6.4
    def encode(%DeferredTransfer{} = t) do
      <<t.sender::m(service_id), t.receiver::m(service_id), t.amount::m(balance), t.memo::binary,
        t.gas_limit::m(gas)>>
    end
  end
end
